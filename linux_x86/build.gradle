
apply plugin: 'cpp'

if(hasProperty('SFML2_DIR'))
{
    model {
        buildTypes {
            debug
            release
        }

        platforms {
            linux_x86 {
                operatingSystem "linux"
                architecture "x86"
            }
        }

        toolChains {
            gcc(Gcc) {
                path "/usr/bin"
                eachPlatform() {
                    cppCompiler.executable "g++"
                }
                target("linux_x86") {
                    cppCompiler.withArguments { List<String> args ->
                        args << "-m32" << "-march=i686" << "-mtune=generic"
                    }
                    linker.withArguments { List<String> args ->
                        args << "-m32"
                    }
                }
            }
        }
    }

    executables {
        game_project {
            targetPlatforms "linux_x86"

            binaries.all {
                cppCompiler.args "-std=c++11", "-Wall", "-Wextra"

                if(hasProperty('ResourcePacker_DIR')) {
                    cppCompiler.define "ResourcePacker_FOUND"
                }

                if(buildType == buildTypes.debug) {
                    cppCompiler.args "-O0", "-g"
                }
                if(buildType == buildTypes.release) {
                    cppCompiler.define "NDEBUG"
                    cppCompiler.args "-O3"
                }
            }
        }
    }

    sources {
        game_project {
            cpp {
                if(hasProperty('ResourcePacker_DIR')) {
                    lib project: ":ResourcePacker", library: "ResourcePacker"
                }
                lib project: ":sfml_linux_x86", library: "sfml_system"
                lib project: ":sfml_linux_x86", library: "sfml_window"
                lib project: ":sfml_linux_x86", library: "sfml_network"
                lib project: ":sfml_linux_x86", library: "sfml_graphics"
                lib project: ":sfml_linux_x86", library: "sfml_audio"
                source {
                    srcDir "${rootDir}/src"
                    include "*.cpp"
                    include "ec/*.cpp"
                }
                exportedHeaders {
                    srcDir "${rootDir}/src"
                    srcDir "${SFML2_DIR}/src/SFML2/headers"
                }
            }
        }
    }
}
else
{
    println "WARNING: SFML game engine builds are unavailable if the"
    println "SFML2_DIR property is undefined!"
}

if(!hasProperty('ResourcePacker_DIR'))
{
    println "WARNING: ResourcePacker_DIR property not defined, will compile"
    println "SFML game engine without it!"
}
